; Franks Player
; Concentrates on building forward Castles and supporting them with Paladins,
; Hand Cannoneers, Throwing Axemen, and a few Halbs. Adds in Bombard Cannons
; and tries to use the Castles to keep them safe and micro them.

; Local search results
(defconst gl-local-total 100)
(defconst gl-local-last 101)
(defconst gl-remote-total 102)
(defconst gl-remote-last 103)

; Locations at which to place the first two additional Town Centers.
(defconst gl-tc-1-x 200)
(defconst gl-tc-1-y 201)
(defconst gl-tc-2-x 202)
(defconst gl-tc-2-y 203)
(defconst gl-num-placed-initial-tcs 204)

; Coordinates of the Wonder.
(defconst gl-wonder-x 210)
(defconst gl-wonder-y 211)

; Flag to indicate whether to process search results for sending units near the
; Wonder to attack it.
(defconst gl-attack-wonder-search-state 220)
(defconst no-search 0)
(defconst search-complete 1)

; Timer to launch repeated attacks once attacking starts.
(defconst t-attack 1)
(defconst attack-interval 60)

; Timer to check for units near the wonder and make them attack.
(defconst t-near-wonder 2)
(defconst near-wonder-interval 45)

; Initializes strategic numbers.
(defrule
  (true)
=>
  ; Enables multiple buildings of the same type to be constructed at once.
  (set-strategic-number sn-enable-new-building-system 1)

  ; Allows dropsites to be adjacent to resources.
  (set-strategic-number sn-allow-adjacent-dropsites 1)

  ; Initializes Villagers to construct buildings.
  (set-strategic-number sn-percent-civilian-explorers 0)
  (set-strategic-number sn-percent-civilian-builders 100)
  (set-strategic-number sn-percent-civilian-gatherers 0)
  (set-strategic-number sn-cap-civilian-explorers 0)
  (set-strategic-number sn-cap-civilian-builders 200)
  (set-strategic-number sn-total-number-explorers 1)
  (set-strategic-number sn-number-explore-groups 1)
  (set-strategic-number sn-maximum-explore-group-size 1)

  ; Initializes Army to defend.
  (set-strategic-number sn-percent-attack-soldiers 0)

  ; Initializes land Villager gatherer percentages.
  (set-strategic-number sn-food-gatherer-percentage 30)
  (set-strategic-number sn-wood-gatherer-percentage 25)
  (set-strategic-number sn-gold-gatherer-percentage 25)
  (set-strategic-number sn-stone-gatherer-percentage 20)

  ; Increases focus of attacks on the Wonder.
	(set-strategic-number sn-special-attack-type1 wonder)

  ; Makes attack groups larger.
  (set-strategic-number sn-maximum-attack-group-size 200)
  (set-strategic-number sn-minimum-attack-group-size 20)

  ; Allows units to patroll while enroute to attacking distant targets.
  (set-strategic-number sn-enable-patrol-attack 1)

  ; Target the human player 1 for attacks.
  (set-strategic-number sn-target-player-number 1)

  ; Enables intelligent attacking.
	(set-strategic-number sn-attack-intelligence 1)

  ; Let's the AI attack walls.
	(set-strategic-number sn-wall-targeting-mode 1)

  (disable-self)
)

; Initializes goals.
(defrule
  (true)
=>
  ; Initializes the TC placement locations.
  (set-goal gl-tc-1-x 156)
  (set-goal gl-tc-1-y 60)
  (set-goal gl-tc-2-x 167)
  (set-goal gl-tc-2-y 54)
  (set-goal gl-num-placed-initial-tcs 0)

  (set-goal gl-attack-wonder-search-state no-search)

  (disable-self)
)

; Resign condition
(defrule
  (military-population < 6)
  (unit-type-count-total villager < 1)
  (not (can-train villager))
=>
  (chat-to-all "The graceless Templars hath mined my Gold! Typical AI.")
  (chat-to-all "No wonder thou wert victorious! I shalt abdicate.")
  (resign)
  (disable-self)
)

; Cheat resources

(defrule
  (game-time >= 660)
=>
  (cc-add-resource food 1000)
  (cc-add-resource wood 1000)
  (cc-add-resource gold 1000)
  (disable-self)
)

(defrule
  (game-time >= 840)
=>
  (cc-add-resource food 1000)
  (cc-add-resource wood 1000)
  (cc-add-resource gold 1000)
  (disable-self)
)

(defrule
  (game-time >= 1020)
=>
  (cc-add-resource food 1000)
  (cc-add-resource wood 1000)
  (cc-add-resource gold 1000)
  (disable-self)
)

(defrule
  (game-time >= 1200)
=>
  (cc-add-resource food 1000)
  (cc-add-resource wood 1000)
  (cc-add-resource gold 1000)
  (disable-self)
)

(defrule
  (game-time >= 1350)
=>
  (cc-add-resource food 1000)
  (cc-add-resource wood 1000)
  (cc-add-resource gold 1000)
  (disable-self)
)

(defrule
  (game-time >= 1500)
=>
  (cc-add-resource food 1000)
  (cc-add-resource wood 1000)
  (cc-add-resource gold 1000)
  (disable-self)
)

(defrule
  (game-time >= 1650)
=>
  (cc-add-resource food 1000)
  (cc-add-resource wood 1000)
  (cc-add-resource gold 1000)
  (disable-self)
)

; Building management.

(defrule
  (building-type-count-total lumber-camp == 0)
  (building-type-count-total town-center == 0)
  (wood-amount < 100)
  (can-buy-commodity wood)
=>
  (buy-commodity wood)
)

(defrule
  (building-type-count-total town-center < 3)
  (stone-amount < 100)
  (can-buy-commodity stone)
=>
  (buy-commodity stone)
)

; Places the first two TCs at set locations.
(defrule
  (up-can-build-line 0 gl-tc-1-x c: town-center-foundation)
=>
  (up-build-line gl-tc-1-x gl-tc-1-x c: town-center-foundation)
  (up-modify-goal gl-num-placed-initial-tcs c:+ 1)
  (disable-self)
)

(defrule
  (up-can-build-line 0 gl-tc-2-x c: town-center-foundation)
=>
  (up-build-line gl-tc-2-x gl-tc-2-x c: town-center-foundation)
  (up-modify-goal gl-num-placed-initial-tcs c:+ 1)
  (disable-self)
)

(defrule
  (building-type-count-total town-center < 3)
  (can-build town-center)
  (goal gl-num-placed-initial-tcs 2)
=>
  (build town-center)
)

(defrule
  (population-headroom > 0)
  (housing-headroom <= 10)
  (up-pending-objects c: house < 3) ; Build at most 3 Houses at once.
  (can-build house)
=>
  (build house)
)

(defrule
  (building-type-count-total monastery < 1)
  (can-build monastery)
=>
  (build monastery)
)

(defrule
  (building-type-count-total stable < 3)
  (can-build stable)
=>
  (build stable)
)

(defrule
  (building-type-count-total siege-workshop < 2)
  (can-build siege-workshop)
=>
  (build siege-workshop)
)

(defrule
  (game-time > 1200)
  (building-type-count-total stable < 10)
  (can-build stable)
=>
  (build stable)
)

(defrule
  (game-time > 1200)
  (building-type-count-total siege-workshop < 7)
  (can-build siege-workshop)
=>
  (build-forward siege-workshop)
)

(defrule
  (building-type-count-total barracks < 2)
  (can-build barracks)
=>
  (build barracks)
)

(defrule
  (building-type-count-total archery-range < 2)
  (can-build archery-range)
=>
  (build archery-range)
)

(defrule
  (building-type-count-total market < 1)
  (can-build market)
=>
  (build market)
)

(defrule
  (or
    (building-type-count-total lumber-camp == 0)
    (and
      (building-type-count-total lumber-camp > 0)
      (and
        (dropsite-min-distance wood > 4)
        (dropsite-min-distance wood < 255)
      )
    )
  )
  (can-build lumber-camp)
=>
  (build lumber-camp)
)

(defrule
  (building-type-count-total lumber-camp < 3)
  (unit-type-count-total villager > 30)
  (can-build lumber-camp)
=>
  (build lumber-camp)
)

; Farms and Mills

(defrule
  (building-type-count-total farm < 80)
  (up-pending-objects c: farm < 5)
  (idle-farm-count < 5)
  (can-build farm)
=>
  (build farm)
)

(defrule
  (building-type-count-total farm > 20)
  (building-type-count-total mill < 3)
  (can-build mill)
=>
  (build mill)
)

; TODO better Mills
; TODO Mining Camps, need to Mine the Stone

; TODO
; Search near the Stone
; If there is Stone but no mining camp, build a mining camp

(defrule
  (unit-type-count-total villager < 100)
  (can-train villager)
=>
  (train villager)
)

; TODO Castles, aggressive and forward (may need to decrease delete percentage)

; TODO
; when base is established, check if there is a Castle on the first forward hill
; if not, place a Castle on that hill

; TODO
; Check if there is a Castle on the first hill.
; If there is, check if there is a Castle in any of the first forward spots.
; If not, pick a spot randomly and place a Castle there.

; TODO
; Check if there is a Castle in any of the first forward spots.
; If so, check if there is a Castle in any of the second forward spots.
; If not, then pick a spot randomly and place a Castle there.

; Unit production

(defrule
  (unit-type-count-total knight-line < 5)
  (can-train knight-line)
=>
  (train knight-line)
)

(defrule
  (unit-type-count-total hand-cannoneer < 10)
  (can-train hand-cannoneer)
=>
  (train hand-cannoneer)
)

(defrule
  (unit-type-count-total bombard-cannon < 3)
  (can-train bombard-cannon)
=>
  (train bombard-cannon)
)

(defrule
  (game-time > 1200)
  (unit-type-count-total bombard-cannon < 25)
  (can-train bombard-cannon)
=>
  (train bombard-cannon)
)

(defrule
  (unit-type-count-total knight-line < 60)
  (can-train knight-line)
=>
  (train knight-line)
)

(defrule
  (unit-type-count-total bombard-cannon < 6)
  (can-train bombard-cannon)
=>
  (train bombard-cannon)
)

(defrule
  (unit-type-count-total throwing-axeman-line < 40)
  (can-train throwing-axeman-line)
=>
  (train throwing-axeman-line)
)

(defrule
  (unit-type-count-total hand-cannoneer < 30)
  (can-train hand-cannoneer)
=>
  (train hand-cannoneer)
)

(defrule
  (unit-type-count-total spearman-line < 40)
  (can-train spearman-line)
=>
  (train spearman-line)
)

; Attack management

(defrule
  (game-time > 300)
=>
  (set-strategic-number sn-percent-attack-soldiers 100)
  (attack-now)
  (enable-timer t-attack attack-interval)
  (enable-timer t-near-wonder near-wonder-interval)
  (disable-self)
)

(defrule
  (timer-triggered t-attack)
=>
  (attack-now)
  (enable-timer t-attack attack-interval)
)

; Periodically tasks units within 30 tiles of the Wonder to attack it.
(defrule
  (timer-triggered t-near-wonder)
=>
  (set-goal gl-attack-wonder-search-state search-complete)
  (up-full-reset-search)
  (set-strategic-number sn-focus-player-number 1)
  (up-find-remote c: wonder c: 1)
  (up-get-search-state gl-local-total)
  (enable-timer t-near-wonder near-wonder-interval)
)

(defrule
  (goal gl-attack-wonder-search-state search-complete)
  (up-compare-goal gl-remote-total > 0)
=>
  (up-set-target-object search-remote c: 0)
  (up-get-point position-object gl-wonder-x)
  (up-set-target-point gl-wonder-x)
  (up-filter-distance c: -1 c: 30)
  (up-find-local c: bombard-cannon c: 60)
  (up-find-local c: knight-line c: 60)
  (up-find-local c: hand-cannoneer c: 60)
  (up-get-search-state gl-local-total)
)

(defrule
  (goal gl-attack-wonder-search-state search-complete)
  (up-compare-goal gl-local-total > 0)
=>
  (up-target-objects 1 action-default -1 -1)
)

(defrule
  (goal gl-attack-wonder-search-state search-complete)
=>
  (set-goal gl-attack-wonder-search-state no-search)
)

; TODO Bombard Cannon micro
